#!/opt/perl/bin/perl
use strict;
use utf8;
use Event;
use AnyEvent;
use Net::XMPP2::Client;
use Net::XMPP2::Ext::Version;

sub append_outputfile {
   open OUT, ">>disco_version.output"
      or die "Couldn't open disco_version.output: $!\n";
   print OUT $_[0];
   close OUT;
}

sub version_req {
   my ($vers, $con, $dest) = @_;

   $vers->request_version ($con, $dest, sub {
      my ($version, $error) = @_;

      if ($error) {
         warn "$dest: DISCO VERSION ERROR $dest: " . $error->string . "\n";
      } else {
         my $from = $version->{jid};
         my $name = $version->{name};
         my $ver  = $version->{version};
         my $os   = $version->{os};
         $ver  =~ s/[\n\t]//g;
         $name =~ s/[\n\t]//g;
         $os   =~ s/[\n\t]//g;
         print "$dest: $from: name: $name version: $ver os: $os\n";
         append_outputfile ("$from\t$name\t$ver\t$os\n");
      }
   });
}

my $j = AnyEvent->condvar;
my $cl = Net::XMPP2::Client->new (debug => 1);
my $vers = Net::XMPP2::Ext::Version->new;
$cl->add_extension ($vers);
my $t = undef;
my @jids = map { chomp; $_ } <STDIN>;
sub mkti {
   my ($con) = @_;
   $t = AnyEvent->timer (after => 1, cb => sub {
      for (1..50) {
         my $j = pop @jids;
         if ($j) {
            version_req ($vers, $con, $j);
         } else {
            print "no more jids to query...\n";
            last;
         }
      }
      mkti ($con);
   });
}
$cl->add_account ('net_xmpp2@jabber.org', 'test', undef, undef, { disable_ssl => 1 });
$cl->reg_cb (
   session_ready => sub {
      my ($cl, $acc) = @_;
      $acc->connection->set_default_iq_timeout (800);
      warn "session ready!\n";
      if (@ARGV) {
         version_req ($vers, $acc->connection, $ARGV[0]);
      } else {
         mkti ($acc->connection);
      }
      $cl->unreg_me
   },
   disconnect => sub {
      my ($cl, $acc, $h, $p, $reas) = @_;
      print "disconnect ($h:$p): $reas\n";
   },
   error => sub {
      my ($cl, $acc, $err) = @_;
      print "ERROR: " . $err->string . "\n";
   },
   message => sub {
      my ($cl, $acc, $msg) = @_;
      print "message from: " . $msg->from . ": " . $msg->any_body . "\n";
   }
);
$cl->start;
$j->wait;
